apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = "${project.ext.group}"
version = "${project.ext.jtranscVersion}"

//tasks.withType(Compile) {
//	options.encoding = 'UTF-8'
//}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

configurations {
	provided
}
sourceSets {
	main { compileClasspath += configurations.provided }
}

repositories {
	mavenLocal()
	mavenCentral()
}

sourceSets {
	main.java.srcDirs = [ 'src' ]
	main.resources.srcDirs = [ 'resources' ]
	test.java.srcDirs = [ 'test' ]
	test.resources.srcDirs = [ 'testresources' ]
}

dependencies {
	testCompile 'junit:junit:4.12'
}

if (System.getenv("JTRANSC_DEPLOY") != null) {
	task javadoc2(type: Javadoc) {
		failOnError = false
	}

	task javadocJar(type: Jar, dependsOn: javadoc2) {
		classifier = 'javadoc'
		from 'build/docs/javadoc'
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}

	artifacts {
		archives jar
		archives javadocJar
		archives sourcesJar
	}

	if(!version.endsWith("-SNAPSHOT")){
		signing {
			sign configurations.archives
		}
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				if(!version.endsWith("-SNAPSHOT")){
					beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
				}
				repository(url: (version.endsWith("-SNAPSHOT")) ? "https://oss.sonatype.org/content/repositories/snapshots/"
                                                                : "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: rootProject.properties['sonatypeUsername'] ?: System.getenv("SONATYPE_USERNAME"),
								   password: rootProject.properties['sonatypePassword'] ?: System.getenv("SONATYPE_PASSWORD"))
				}

				pom.project {
					name "${project.name}"
					packaging 'jar'
					description 'JVM AOT compiler currently generating JavaScript, C++, Haxe, with initial focus on Kotlin and games.'
					url 'https://github.com/jtransc/jtransc/'
					inceptionYear '2015'

					scm {
						url 'scm:git@github.com:jtransc/jtransc.git'
						connection 'scm:git@github.com:jtransc/jtransc.git'
						developerConnection 'scm:git@github.com:jtransc/jtransc.git'
					}

					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id 'soywiz'
							name 'Carlos Ballesteros Velasco'
						}
					}
				}
			}
		}
	}

//println("uploadArchives!: ${project.name}")
}
